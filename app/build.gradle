buildscript {
    repositories {
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.+'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'io.fabric'

repositories {
    maven { url 'https://maven.fabric.io/public' }
}

ext {
    developerId = 'drxeno02'
    developerName = 'Leonard Tatum'
    developerEmail = 'ljtatum@hotmail.com'

    siteUrl = 'https://github.com/drxeno02/ubuyismile'
    gitUrl = 'https://github.com/drxeno02/ubuyismile.git'
}

android {
    compileSdkVersion 27

    // construct version name
    def versionMajor = 1
    def versionMinor = 0
    def versionPatch = 0
    def code = 1

    defaultConfig {
        applicationId "com.blog.ljtatum.ubuyismile"
        versionName "${versionMajor}.${versionMinor}.${versionPatch}"
        minSdkVersion 18
        targetSdkVersion 27
        versionCode code

        // enabling multidex support
        multiDexEnabled true
        //noinspection GroovyAssignabilityCheck
        signingConfig signingConfigs.debug
    }
    // specifies a flavor dimension
    flavorDimensions "app"
    buildTypes {
        debug {
            applicationIdSuffix '.debug'
            buildConfigField "String", "ENVIRONMENT", "\"test\""
            buildConfigField "boolean", "DEBUG_MODE", "true"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]

            // debuggable
            debuggable true
        }
        release {
            buildConfigField "String", "ENVIRONMENT", "\"production\""
            buildConfigField "boolean", "DEBUG_MODE", "false"
            manifestPlaceholders = [build_version: "${versionMajor}.${versionMinor}.${versionPatch}"]

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            proguardFile 'proguard-rules.pro'
        }
    }
    // flavor implementation propelled by request for multiple environments on Crashlytics
    productFlavors {
        standard {
            applicationId 'com.blog.ljtatum.ubuyismile'
            dimension "app"
        }
        beta {
            applicationId 'com.blog.ljtatum.ubuyismile.beta'
            dimension "app"
        }
    }
    lintOptions {
        // if true, stop the gradle build if errors are found
        abortOnError false
    }
}

dependencies {
    api project(':AmazonAffiliateFramework')
    api project(':ApplicationFramework')
    // crashlytics
    //noinspection GradleCompatible
    api('com.crashlytics.sdk.android:crashlytics:2.9.2@aar') {
        transitive = true;
    }
}

// THIS IS REQUIRED FOR FIREBASE (KEEP HERE)
apply plugin: 'com.google.gms.google-services'
